/*
 * Copyright 2018 Wultra s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 */

apply plugin: 'maven'
apply plugin: 'signing'

def groupId = project.GROUP_ID
def artifactId = project.ARTIFACT_ID
def version = project.VERSION_NAME

def localReleaseDest = "${buildDir}/release/${version}"

task androidJavadocs(type: Javadoc) {
    enabled = false // we have to disable because there are no java sources, only kotlin
    excludes = ['**/*.kt'] // exclude kotlin, Javadoc task can't handle that
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // adds libraries to classpath
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.getJavaCompiler().classpath
        }
    }
    options.addStringOption('encoding', 'UTF-8')
}

task androidJavadocsJar(type: Jar, dependsOn: [androidJavadocs, dokka]) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
    from dokka.outputDirectory
}

task androidSourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

uploadArchives {
	repositories.mavenDeployer {
		pom.groupId = groupId
		pom.artifactId = artifactId
		pom.version = version
		// Add other pom properties here if you want (developer details / licenses)
		pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
		repository(url: "file://${localReleaseDest}")
	}
}

task zipRelease(type: Zip) {
	from localReleaseDest
	destinationDir buildDir
	archiveName "release-${version}.zip"
}

task generateRelease {
    doLast {
        println "Release ${version} can be found at ${localReleaseDest}/"
        println "Release ${version} zipped can be found ${buildDir}/release-${version}.zip"
    }
}

generateRelease.dependsOn(uploadArchives)
generateRelease.dependsOn(zipRelease)
zipRelease.dependsOn(uploadArchives)

artifacts {
	archives androidSourcesJar
	archives androidJavadocsJar
}



// signing

if (project.hasProperty('signing.keyId')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
        }
    }
}
